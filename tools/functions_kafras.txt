//===== rAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 6.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================ 

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome){
		default:
		case 0:
			// Default message (obsolete)
			mes "[Kafra Employee]";
			mes "ยินดีต้อนรับสู่";
			mes "Kafra Corporation.";
			mes "บริการของ Kafra";
			mes "อยู่เคียงข้างเจ้าเสมอ.";
			mes "มีอะไรให้ข้ารับใช้ไหม ?";
			break;
		case 1:
			// Niflheim specific message
			mes "[Kafra Employee]";
			mes "^666666ย-ยินนนนด-ดี-ต้อนรับ";
			mes "สู่ K-kaaafrrrra";
			mes "C-coorpoor-r-ratioooonn...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			set @GID,getcharid(2);
			mes "[Kafra Employee]";
	 		mes "ยินดีต้อนรับ.สมาชิก ^ff0000" + GetGuildName(@GID) + "^000000.";
			mes "Kafra Coporation อยู่ให้บริการเจ้า.";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[Kafra Employee]";
			mes "เจ้ามาจากดินแดนที่ห่างไกลเพื่อศึกษาวัฒนธรรมของเราหรือเพียงแค่เที่ยวชม ?";
			mes "ไม่ว่าอย่างไหน, ทำไมไม่พักที่นี้ละ ?";
			mes "อากาศก็บริสุทธิ์";
			mes "แถมไปด้วยหมู่มวลดอกไม้.";
			break;		
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[Kafra Employee]";
			mes "ด้วยบริการของ Kafra";
			mes "ท้องถิ่น, ทำให้เจ้าไม่";
			mes "คุ้นเคยน่ะสิ.";
			break;		
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (1) {
		deletearray @K_Menu0$[0],getarraysize(@K_Menu0$);
		if (.@welcome == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"เปิดคลัง","เปิดคลังกิลด์","เช่ารถเข็น","ใช้บริการ Teleport","ยกเลิก";
		} else {
			switch(.@menu_num){
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"เซฟ","เปิดคลัง","ยกเลิก"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"เปิดคลัง","ยกเลิก"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"เซฟ","เปิดคลัง","เช่ารถเข็น","ตรวจสอบข้อมูล","ยกเลิก"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"เปิดคลัง","เช่ารถเข็น","ตรวจสอบข้อมูล","ยกเลิก"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"เปิดคลัง","ตรวจสอบข้อมูล","ยกเลิก"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"เซฟ","เปิดคลัง","เช่ารถเข็น","ยกเลิก"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"เซฟ","เปิดคลัง","ตรวจสอบข้อมูล","ยกเลิก"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"เปิดคลัง","เช่ารถเข็น","ใช้บริการ Teleport","ตรวจสอบข้อมูล","ยกเลิก"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"เปิดคลัง","เซฟ","เช่ารถเข็น","ยกเลิก"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"เซฟ","เปิดคลัง","ใช้บริการ Teleport","เช่ารถเข็น","ตรวจสอบข้อมูล","ยกเลิก"; break;
			}
		}
		set .@menu$,"";
		for (set .@i, 0; .@i < getarraysize(@K_Menu0$); set .@i, .@i + 1)
			set .@menu$, .@menu$ + @K_Menu0$[.@i] + ":";	
		set .@j, select (.@menu$) - 1;
		if (@K_Menu0$[.@j] == "เซฟ") {
			return;
		} else if (@K_Menu0$[.@j] == "เปิดคลัง") {
			// Do not charge for Guild Storage
			if(.@welcome == 2) 
				callfunc "F_KafStor",2,0,0;
			else 
				callfunc "F_KafStor",0,getarg(3),.@welcome;
			next;
		} else if (@K_Menu0$[.@j] == "ใช้บริการ Teleport") {
			// Display Einbroch "No Teleport Service" notice.
			if(.@menu_num != 4) callfunc "F_KafTele",.@welcome;
			else {
				mes "[Kafra Employee]";
				mes "เพราะว่า ^FF0000ข้อจำกัด";
				mes "ในการขนส่ง^000000,";
				mes "Kafra Corporation ไม่สามารถ";
				mes "ให้บริการ Teleport";
				mes "ใน Schwaltzvalt Republic ได้.";
				next;
				mes "[Kafra Employee]";
				mes "เจ้าสามารถ";
				mes "ใช้บริการ Airship";
				mes "แทนได้. ขอบคุณที่";
				mes "เข้าใจในบริการของเรา.";
			}
			next;
		} else if (@K_Menu0$[.@j] == "เช่ารถเข็น") {
			if(callfunc("F_KafCart",.@welcome,getarg(4)) == 1) 
				next;
		} else if (@K_Menu0$[.@j] == "ตรวจสอบข้อมูล") {
			callfunc "F_KafInfo",getarg(2);
			next;
		} else if (@K_Menu0$[.@j] == "ยกเลิก"){
			callfunc "F_KafEnd",.@welcome,0;
			end;
		} else if (@K_Menu0$[.@j] == "เปิดคลังกิลด์") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if(.@type == 1){
		if(guildopenstorage()){
			mes "[Kafra Employee]";
			mes "ข้าขอโทษด้วยมีสมาชิกคนอื่นในกิลด์กำลังใช้คลังอยู่";
			mes "กรุณารอจนกว่าคนนั้นจะทำเสร็จสิ้น.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(!callfunc("F_CanOpenStorage")){
		mes "[Kafra Employee]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666ข-ข-ขขขอโทษ,";
			mes "เ-เจ้า เ-เ-เป็นนน";
			mes "Nooviiice... ต-ต้องการ";
			mes "พ-พื้นฐาน sssskill l-level 6...^000000";
			return;
		}
		mes "ขอโทษด้วย, แต่เจ้า";
		mes "ต้องมี Skill";
		mes "พื้นฐานของ Novice ที่ Level 6";
		mes "ถึงจะเปิดคลังได้.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(.@type != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<.@fee){
				mes "[Kafra Employee]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "ม-มากกว่านี้...!";
					mes "ต-ต้องการรรร 150... zeny...";
					mes "อ้ากก! เ-เลือดดด ล-ลลดดด~!^000000";
					return;
				}
				// Standard Message
				mes "ขอโทษด้วย, แต่เจ้า";
				mes "มีเงินไม่พอที่จะ";
				mes "เปิดคลัง.";
				mes "ค่าธรรมเนียมคือ "+.@fee+" zeny.";
				return;
			}
			set Zeny, Zeny-.@fee;
			set RESRVPTS, RESRVPTS + (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Kafra Employee]";
			for (set .@i, 0; .@i < 5; set .@i, .@i + 1)
				mes "^666666ขอบคุณ.. สำหรับ... ที่ใช้...บริ....^000000";
		}
		// Normal message
		else {
			mes "[Kafra Employee]";
			mes "ข้าได้เปิด";
			mes "คลังของเจ้าให้แล้ว.";
			mes "ขอบคุณที่ใช้";
			mes "บริการของ Kafra Service.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[Kafra Employee]";
	mes "กรุณาเลือกสถานที่";
	mes "ปลายทางของเจ้า.";
	next;
	set .@menu$, "";
	for (set .@i, 0; .@i <= 6; set .@i, .@i + 1)
		set .@menu$, .@menu$ + @wrpC$[.@i] + ":";
	set .@j, select (.@menu$) - 1;
	if (@wrpC$[.@j] == "Cancel") return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
	else {
		if (Zeny<@wrpP[.@j]){
			mes "[Kafra Employee]";
			mes "ขอโทษด้วย, แต่เจ้า";
			mes "มีเงินไม่พอที่จะ Teleport";
			mes "ค่าบริการ teleport";
			mes "ที่ "+@wrpD$[.@j]+" คือ "+@wrpP[.@j]+" zeny.";
			close2;
			cutin "", 255;
			end;
		}
		set Zeny, Zeny - @wrpP[.@j];
		set RESRVPTS, RESRVPTS + (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") { if (checkre(0)) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mjolnir Dead Pit") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Orc Dungeon") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(BaseClass != Job_Merchant){
		mes "[Kafra Employee]";
		mes "ขอโทษด้วย, แต่เจ้า";
		mes "เช่ารถเข็นไม่ได้";
		if (checkre(0))
			mes "เฉพาะอาชีพ Merchant เท่านั้น.";
		else {
			mes "เฉพาะอาชีพ Merchants,";
			mes "Blacksmiths, Master Smiths,";
			mes "Alchemists และ Biochemists.";
		}
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[Kafra Employee]";
		mes "เจ้าพร้อมที่จะ";
		mes "มีรถเข็นแล้ว.";
		mes "น่าเสียดาย, ข้าไม่สามารถ";
		mes "ให้เจ้าเช่ารถเข็น";
		mes "มากกว่าหนึ่งคันได้.";
		return 1;
	}
	// Check if the player has the Pushcart skill
	else if(getskilllv("MC_PUSHCART") == 0) {
		mes "[Kafra Employee]";
		mes "เจ้าจะเช่ารถเข็นได้หลังจากเรียนทักษะ \"Push Cart\" skill.";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Kafra Employee]";
		mes "เช่ารถเข็น";
		mes "ค่าธรรมเนียมคือ "+getarg(1)+" zeny.";
		mes "เจ้าจะเช่ารถเข็นไหม ?";
		next;
		if (select("เช่ารถเข็น.:ยกเลิก") == 2) return 0;
			if(Zeny<getarg(1)){
				mes "[Kafra Employee]";
				mes "ขอโทษด้วย, แต่เจ้า";
				mes "ไม่มีเงินพอที่จะ";
				mes "เช่ารถเข็น";
				mes "ค่าธรรมเนียมคือ "+getarg(1)+" zeny.";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//	set .@block, 1;
		setarray .@m$, "ตรวจสอบคะแนน Special Reserve.","บริการพาสเวิร์ดคลัง","ตำแหน่งพนักงาน Kafra","ยกเลิก";
		if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight
		else if (.@block) deletearray .@m$[1],1;
	while (1) {
		set .@j, select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "ตรวจสอบคะแนน Special Reserve.") {
			mes "[Kafra Employee]";
			mes "มาดูกัน...";
			mes strcharinfo(0) + "...";
			mes "อ่า, เจ้ามีทั้งหมด";
			mes RESRVPTS+ " Special Reserve Points.";
			next;
			mes "[Kafra Employee]";
			mes "เจ้าสามารถแลกเปลี่ยน";
			mes "คะแนน Special Reserve กับ";
			mes "ของรางวัลที่ Kafra Main Office ใน Al De Baran. กรุณาใช้";
			mes "บริการที่สะดวกเพื่อรับของรางวัล.";
			next;
			if(getarg(0) == 1) return;
		} else if (.@m$[.@j] == "ตำแหน่งพนักงาน Kafra") {
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "บริการพาสเวิร์ดคลัง") {
			callfunc("F_SetKafCode","[Kafra Employee]","Kafra Services");
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Kafra Employee]";
	if(getarg(1)==1) {	
		// Save
		mes "จุดเกิดของเจ้า";
		mes "ได้บันทึกที่นี้แล้ว";
		mes getarg(2)+".";
		mes "ขอบคุณที่ใช้";
		mes "บริการของ Kafra Services.";
	} else if(getarg(0)==0 || getarg(0)==5) {	
		// Generic End
		mes "พวกเราคือ Kafra Corporation,";
		mes "พยายามที่จะให้บริการที่ดีที่สุดแก่เจ้าเสมอ ข้าหวังว่าเจ้าจะได้พบกับความต้องการและมาตรฐานที่ดีในการให้บริการ.";
	} else if(getarg(0)==1) {
		// Niflheim End
		percentheal 0,- 25;
		mes "^666666Kaffffra ไ-ไม่เคย";
		mes "ตาาาาาย. เคียง...";
		mes "เคียง ข-ข้าาาาง เจ้าาาาาา~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if(!#kafra_code) return;
	mes "ใส่รหัสคลังของเจ้า:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom "รหัสคลังผิด.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if(#kafra_code) {
		mes "คลังของเจ้าจะได้รับการป้องกันจากรหัสผ่าน. เจ้าจะทำเลยไหม ?";
		next;
		switch (select("เปลี่ยนรหัสผ่าน -> 5000z:ลบรหัสผ่าน -> 1000z:ยกเลิก")) {
			case 1:
				mes .@npc_name$;
				mes "กรุณาใส่ ^0000FFรหัสเก่า^000000 ของเจ้า.";
				set @code,callfunc("F_EntKafCode");
				if(!@code || @code != #kafra_code) {
					mes "รหัสผิด. เจ้าไม่สามารถกำหนดรหัสใหม่ได้.";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,.@npc_name$,.@comp_name$;
				break;
			case 2:
				mes .@npc_name$;
				mes "กรุณาใส่รหัสผ่านของเจ้าก่อนที่จะลบ.";
				set @code,callfunc("F_EntKafCode");
				if(!@code) {
					mes "รหัสได้ถูกลบไปแล้ว.";
					emotion e_hmm;
					break;
				}
				next;
				mes .@npc_name$;
				if(Zeny < 1000) {
					mes "เจ้ามีเงินไม่พอ.";
					emotion e_cash;
					break;
				}
				set Zeny,Zeny-1000;
				if(@code == #kafra_code) {
					set #kafra_code,0;
					mes "เจ้าได้ลบรหัสผ่านแล้ว.";
					mes "ขอบคุณที่ใช้ "+.@comp_name$+".";
					emotion e_thx;
				} else {
					mes "รหัสผิด. ข้าไม่สามารถคืนเงิน 1000z ให้ได้.";
					mes "กรุณาลองใหม่ในครั้งหน้า.";
					emotion e_sry;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes .@comp_name$+" ขอนำเสนอบริการใหม่:";
		mes "ใส่รหัสผ่านให้กับคลัง.";
		next;
		switch (select("กำหนดรหัสใหม่ -> 5000z:ยกเลิก")) {
			case 1: callsub S_SET,.@npc_name$,.@comp_name$; break;
			case 2: break;		
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes .@npc_name$;
	mes "กรุณาใส่ ^FF0000รหัสใหม่^000000 เพื่อป้องกันคลังของเจ้าจากพวกโจร.";
	set @code,callfunc("F_EntKafCode");
	if(!@code) {
		mes "ยังไม่ได้เปลี่ยนรหัสผ่าน.";
		emotion e_hmm;
		return;
	}
	next;
	mes .@npc_name$;
	if(Zeny < 5000) {
		mes "เจ้ามีเงินไม่พอ.";
		emotion e_cash;
		return;
	}
	set Zeny,Zeny-5000;
	set #kafra_code,@code;
	mes "คลังของเจ้าจะได้รับการป้องกันจากรหัสผ่านแล้ว.";
	mes "ขอบคุณที่ใช้ "+.@comp_name$+".";
	emotion e_thx;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "ใส่ตัวเลข 1000~10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "ลองปรับรหัสผ่าน.";
	}
	if(input(@code_) == 1) {
		mes "รหัสของเจ้ายาวเกินไป.";
		return 0;
	}
	if(@code_<1000) {
		mes "รหัสของเจ้าสั้นเกินไป.";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	cleararray @wrpD$[0],"",getarraysize(@wrpD$);
	cleararray @wrpP$[0],"",getarraysize(@wrpP$);
	cleararray @viewpX[0],"",getarraysize(@viewpX);
	cleararray @viewpY[0],"",getarraysize(@viewpY);
	if (strnpcinfo(4) == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Orc Dungeon", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (strnpcinfo(4) == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (strnpcinfo(4) == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	} else if (strnpcinfo(4) == "comodo" ) {
		setarray @wrpD$[0], "Morroc", "Comodo Pharos Beacon", "Umbala";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (strnpcinfo(4) == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morroc";
		setarray @wrpP[0], 1200, 1200;
	} else if (strnpcinfo(4) == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (strnpcinfo(4) == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(4) == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (strnpcinfo(4) == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (strnpcinfo(4) == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morroc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (strnpcinfo(4) == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (strnpcinfo(4) == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
			setd "@wrpP["+.@i+"]",getd("@wrpP["+.@i+"]") * 2;
	}

	for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
		setd "@wrpC$["+.@i+"]",getd("@wrpD$["+.@i+"]")+" -> "+getd("@wrpP["+.@i+"]")+" z";
	setd "@wrpC$["+getarraysize(@wrpC$)+"]","Cancel";
	return;
}
